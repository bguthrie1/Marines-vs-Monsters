actor ArachPlasma : MonsterWeapon
{
  obituary "%o let %k get %h."
  States
  {
  Ready:
    PLSG A 1 A_WeaponReady
    loop
  Deselect:
    PLSG A 1 A_Lower
    loop
  Select:
    PLSG A 0 A_Raise
    PLSG A 0 A_Raise
    PLSG A 0 A_Raise
    PLSG A 0 A_Raise
    PLSG A 1 A_Raise
    loop
  Fire:
    PLSG A 20
  Hold:
    PLSG A 0 A_GunFlash
    PLSF A 4 A_FireCustomMissile("ArachnotronPlasma")
    PLSF B 5
    PLSG A 0 A_ReFire
    goto Ready

  }
}

actor ArachPlayer : PlayerMonster
{
  player.startitem "ArachPlasma"
  player.displayname "Arachnotron"
  player.viewheight 45
  Player.SoundClass "baby2" // Custom sound classes used primarly for *usefail and *grunt.
  obituary "%o let %k get %h."
  health 500
  radius 64
  height 64
  mass 600
  speed 0.3
  painchance 128
  DamageFactor "ArachnoFire", 0
  states
  {
    Spawn:
		  BSPI A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
      BSPI A 0 Thing_ChangeTID(0, 1)
      BSPI A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
      BSPI A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
      BSPI A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
      BSPI A 0 A_ChangeFlag ("SHOOTABLE", 1)
      BSPI A 0 A_GiveInventory("NoTargetAlreadySet", 1)
      BSPI A 0 A_GiveInventory ("ArachPlasma") // Incase the map changed during the pain state, otherwise the weapon would be lost
      BSPI A 0 A_SelectWeapon ("ArachPlasma")
      BSPI AB 10
      BSPI A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
      Goto Spawn
      BSPI A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
      Goto Spawn
      BSPI A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
      loop
    See:
		  BSPI A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
      BSPI A 0 Thing_ChangeTID(0, 1)
      BSPI A 3 A_PlaySound("baby/walk")
      BSPI ABBCC 3
      BSPI D 3 A_PlaySound("baby/walk")
      BSPI DEEFF 3
      BSPI D 0 A_Jump (20, "PlayActiveRoar")
      BSPI D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
      Goto See
      BSPI D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
      Goto See
      BSPI D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
      loop
    Missile:
      BSPI A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
      BSPI A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
      BSPI A 20 BRIGHT
    Melee:
      BSPI G 4 BRIGHT
      BSPI H 6 BRIGHT
      BSPI A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
      Goto Spawn
    Pain:
      BSPI A 0 A_TakeInventory ("ArachPlasma", 1) // Take away weapon to stop the monster firing while stunned
      BSPI A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
      BSPI I 3
      BSPI I 3 A_PlaySound("baby/pain")
      BSPI A 0 A_GiveInventory ("ArachPlasma", 1)
      BSPI A 0 A_SelectWeapon ("ArachPlasma")
      Goto Spawn
    Death:
      BSPI J 20 A_PlayerScream
      BSPI K 7 A_NoBlocking
      BSPI LMNO 7
      BSPI P -1
      Stop
    Raise:
      BSPI P 5
      BSPI ONMLKJ 5
      Goto See+1

    PlayActiveRoar:
    BSPI A 0 A_PlaySound("baby/active")
    Goto See

    PlaySightRoar:
    BSPI A 0 A_PlaySound("baby/sight") // Rawr.
    Goto Spawn
  }
}
