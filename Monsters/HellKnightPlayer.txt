actor BaronBall3 : BaronBall2
{
  Obituary "%o was gutted by %k."
}


actor HellKnightPlayer : BaronOfHellPlayer
{
  Player.Startitem "BaronBall3" // So that the monster player can shoot stuff.
  Player.Displayname "Hell Knight" // Name shown on menu display.
  Player.SoundClass "knight2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o was slayed by %k."
  health 500
  -DONTMORPH
  -BOSSDEATH

  states
  {
  Spawn:
	  BOS2 A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    BOS2 A 0 Thing_ChangeTID(0, 1)
    BOS2 A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    BOS2 A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    BOS2 A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    BOS2 A 0 A_ChangeFlag ("SHOOTABLE", 1)
    BOS2 A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    BOS2 A 0 A_GiveInventory ("BaronBall3") // Incase the map changed during the pain state, otherwise the weapon would be lost
    BOS2 A 0 A_SelectWeapon ("BaronBall3")
    BOS2 AB 10
    BOS2 A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    BOS2 A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    BOS2 A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop

  See:
	  BOS2 A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    BOS2 A 0 Thing_ChangeTID(0, 1)
    BOS2 AABBCCDD 3
    BOS2 D 0 A_Jump (20, "PlayActiveRoar")
    BOS2 D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    BOS2 D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    BOS2 D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop

  Melee:

  Missile:
    BOS2 A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    BOS2 A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    BOS2 EF 8
    BOS2 G 8
    BOS2 A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn

  Pain:
    BOS2 A 0 A_TakeInventory ("BaronBall3", 1) // Take away weapon to stop the monster firing while stunned
    BOS2 A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he is in pain. This is to mimic stunning.
    BOS2 A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster is in pain.
    BOS2 H 2
    BOS2 H 2 A_PlaySound("knight/pain")
    BOS2 A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    BOS2 A 0 A_GiveInventory ("BaronBall3", 1)
    BOS2 A 0 A_SelectWeapon ("BaronBall3")
    goto See

  Death:
    BOS2 I 8
    BOS2 J 8 A_PlayerScream
    BOS2 K 8
    BOS2 L 8 A_NoBlocking
    BOS2 MN 8
    BOS2 O -1
    stop

 Raise:
    BOS2 ONMLKJI 8
    goto See

  PlayActiveRoar:
    BOS2 A 0 A_PlaySound("knight/active")
    Goto See

  PlaySightRoar:
    BOS2 A 0 A_PlaySound("knight/sight") // Rawr.
    Goto Spawn
  }
}
