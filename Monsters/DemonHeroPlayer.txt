Actor DemonHeroPlayer : BaronOfHellPlayer
{
   +ISMONSTER
   -COUNTKILL
   +PICKUP
   Player.ColorRange 16, 47 // Allows players to change their hero color
   Painchance 60
   Health 500
   Radius 16
   Height 56
   Scale 0.8
   Player.SideMove 1
   Player.Viewheight 48
   Player.Displayname "Demon Hero" // Name shown on menu display.
   Player.Startitem "BaronBallHero" // Concentrated long range attack
//   Player.Startitem "BaronBallSpread" // Shotgun like attack
//   Player.Startitem "BaronBallCharged" // Powerful attack, but dangerous in close quarters
//   Player.Startitem "BaronBallRapid" // Rapid attack
//   Player.Startitem "BaronBallSnipe" // extremely fast projectile
//   Player.Startitem "BaronShieldMVM" // Defensive ability, weakens the player afterwards
   Speed 1

   States
   {
    Spawn:
	  BOSS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team.
    BOSS A 0 Thing_ChangeTID(0, 1)
    BOSS A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    BOSS A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    BOSS A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    BOSS A 0 A_ChangeFlag ("SHOOTABLE", 1)
    BOSS A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    BOSS A 0 A_JumpIf (health > 100, 2)
    BOSS A 0 A_GiveInventory ("DemonHeal", 1)
    BOSS AB 10
    loop

    See:
	  BOSS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team.
    BOSS A 0 Thing_ChangeTID(0, 1)
    BOSS AABBCCDD 3
    BOSS A 0 A_JumpIf (health > 100, 2)
    BOSS A 0 A_GiveInventory ("DemonHeal", 1)
    BOSS D 0 A_Jump (20, "PlayActiveRoar")
    loop

    Missile:
    BOSS EF 8
    BOSS G 8
    goto Spawn

    Pain:
    BOSS H 2
    BOSS H 2 A_PlaySound("baron/pain")
    goto Spawn

    PlayActiveRoar:
    BOSS A 0 A_PlaySound("baron/active")
    Goto See

    PlaySightRoar:
    BOSS A 0 A_PlaySound("baron/sight") // Rawr.
    Goto Spawn


   }
}

Actor DemonHeal : Health
{
	Game Doom
	Inventory.Amount 10
	Inventory.MaxAmount 500

}

Actor BaronBallHero : BaronBall2
{
  Weapon.SlotNumber 1
  States
  {
  Fire:
    PUNG B 8 DamageThing(5)
    PUNG C 8
    PUNG D 8 A_FireCustomMissile("BaronBallHeroProjectile")
    PUNG C 3
    PUNG B 15 A_ReFire
    goto Ready
  }
}

actor BaronBallHeroProjectile : BaronBall
{
    Speed 30
    Damage 8

		DamageType "BaronHeroFire"
}


Actor BaronBallSpread : BaronBall2
{
  Weapon.SlotNumber 2
  obituary "%o was ripped open by %k."
  States
  {
  Fire:
    PUNG B 8 DamageThing(5)
    PUNG C 8
    PUNG D 8
    PUNG A 0 A_FireCustomMissile("BaronBallSpreadProjectile", 11.25)
    PUNG A 0 A_FireCustomMissile("BaronBallSpreadProjectile")
    PUNG A 0 A_FireCustomMissile("BaronBallSpreadProjectile", -11.25)
    PUNG C 3
    PUNG B 15 A_ReFire
    goto Ready

  }
}

actor BaronBallSpreadProjectile : BaronBallHeroProjectile
{
    Speed 25
    Damage 8
    Scale 0.8
    States
    {
    Spawn:
    BAL7 AB 4 Bright
    Death:
    BAL7 CDE 4 Bright
    Stop
    }
}


Actor BaronBallCharged : MonsterWeapon
{
  Weapon.SlotNumber 5
  obituary "%o was ripped open by %k."
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 1 A_Raise
    loop
  Fire:
    PUNG B 6 DamageThing(50)
    PUNG C 6
    PUNG D 6
    PUNG A 0 A_FireCustomMissile("BaronBallChargedProjectile")
    PUNG C 3
    PUNG B 0 A_ReFire
    goto Ready

  }
}

actor BaronBallChargedProjectile : FatShot
{
    Translation "208:235=112:127", "160:191=112:127", "16:47=112:127", "128:151=112:127"
//    DamageType "BaronHeroFire"
    DamageType "BaronFire2"
		Radius 20
    Height 25
    Speed 20
    Damage 50
    Scale 3

    States
    {
      Death:
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
    }
}

Actor BaronBallSnipe : BaronBall2
{
  Weapon.SlotNumber 4
  States
  {
  Fire:
    PUNG B 2 DamageThing(20)
    PUNG C 2
    PUNG D 2 A_FireCustomMissile("BaronBallSnipeProjectile")
    PUNG C 15
    PUNG B 0 A_ReFire
    goto Ready
  }
}

actor BaronBallSnipeProjectile : PlasmaBall2
{
    Translation "176:191=112:127", "232:239=112:127", "208:231=112:127", "168:175=112:127", "16:47=112:127", "48:79=112:127", "128:159=112:127"
    Decal "BaronScorch"
	BounceType None
	DamageType "BaronFire2"
    Speed 60
    Damage 12
}



Actor BaronBallRapid : MonsterWeapon
{
  Weapon.SlotNumber 3
  obituary "%o was ripped open by %k."
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 1 A_Raise
    loop
  Fire:
    PUNG B 1
    PUNG C 1
    PUNG D 1
    PUNG A 0 A_FireCustomMissile("BaronBallRapidProjectile")
    PUNG C 1
    PUNG B 0 A_ReFire
    goto Ready

  }
}

actor BaronBallRapidProjectile : BaronBallHeroProjectile
{
    BounceType None
		DamageType "BaronFire2"
    Speed 30
    Damage 2
    Radius 13
    Height 8


  States
  {
  Spawn:
    PLS1 AB 6 Bright
    Loop
  Death:
    PLS1 CDEFG 4 Bright
    Stop
  }
}

actor InvulnerableFogHero : TeleportFog
{
   Translation "112:127=176:191"
   +SHOOTABLE
   -NOBLOCKMAP
   +INVULNERABLE
   +REFLECTIVE
   Radius 16
   Height 56
   Health 1000000
   States
   {
   Spawn:
    TNT1 A 0
	TFOG AB 1 Bright A_Warp (AAPTR_TARGET, 24, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
	TFOG A 0 A_CheckFlag("REFLECTIVE", "Spawn", AAPTR_TARGET)
	TFOG A 0
    Goto Death

	Death:
	TFOG CDEFGHIJ 2 Bright
	Stop
   }
}

Actor BaronShieldMVM : BaronBall2
{
  // The Baron shield makes one invulnerable, but only from the front.
  Weapon.SlotNumber 6
  States
  {
  Fire:
    PUNG B 2 A_ChangeFlag("REFLECTIVE", 1)
    PUNG C 2 A_ChangeFlag("SHIELDREFLECT", 1) // Shield goes for 10 seconds
    PUNG D 350 A_SpawnItemEx("InvulnerableFogHero", 24, 0, 0, 0, 0, 0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
    PUNG C 2 A_ChangeFlag("SHIELDREFLECT", 0)
	PUNG B 2 A_ChangeFlag("REFLECTIVE", 0)
	PUNG B 350 // Player cannot fire or change weapons for 10 seconds
    PUNG B 0 A_ReFire
    goto Ready
  }
}


