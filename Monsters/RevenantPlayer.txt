actor SkelAttack2 : MonsterWeapon
{
  Obituary "%o was punched by %k."
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 1 A_Raise
    loop
  Fire:
    TNT1 B 5 A_JumpIfCloser(75,"MeleeAttack")
    TNT1 C 5
    TNT1 D 10 A_FireCustomMissile("RevenantTracer", 0, 0, 0, 20)
    TNT1 C 5
    TNT1 B 5 A_ReFire
    goto Ready
  MeleeAttack:
    PUNG B 1 A_PlaySound("skeleton/swing")
    PUNG C 6
    PUNG D 6 A_PlaySound("skeleton/melee")
    PUNG C 6 A_CustomPunch(6 * random(1, 10), 1) // True Revenant Melee damage.
    PUNG B 0 A_ReFire
    goto Ready
  }
}

actor RevenantPlayer : PlayerMonster
{
  health 300
  radius 20
  height 56
  mass 500
  speed 0.33
  painchance 100
  Player.Viewheight 65
  player.displayname "Revenant"
  player.startitem "SkelAttack2"
  Player.SoundClass "skeleton2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o couldn't evade %k's fireball."
  DamageFactor "RevenantFire", 0
  +FLOORCLIP

  states
  {
  Spawn:
	  SKEL A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SKEL A 0 Thing_ChangeTID(0, 1)
    SKEL A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    SKEL A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    SKEL A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    SKEL A 0 A_ChangeFlag ("SHOOTABLE", 1)
    SKEL A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    SKEL A 0 A_GiveInventory ("SkelAttack2") // Incase the map changed during the pain state, otherwise the weapon would be lost
    SKEL A 0 A_SelectWeapon ("SkelAttack2")
    SKEL AB 10
    SKEL A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    SKEL A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    SKEL A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
	  SKEL A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SKEL A 0 Thing_ChangeTID(0, 1)
    SKEL AABBCCDDEEFF 2
    SKEL D 0 A_Jump (20, "PlayActiveRoar")
    SKEL D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    SKEL D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    SKEL D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Melee:
    SKEL A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    SKEL A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SKEL G 1
    SKEL G 6
    SKEL H 6
    SKEL I 6
    SKEL A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn
  Missile:
    SKEL A 0 A_JumpIfCloser (75,"Melee")
    SKEL A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    SKEL A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SKEL J 1 Bright
    SKEL J 9 Bright
    SKEL K 10
    SKEL K 10
    SKEL A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn
  Pain:
    SKEL A 0 A_TakeInventory ("SkelAttack2", 1) // Take away weapon to stop the monster from firing while stunned
    SKEL A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    SKEL L 5
    SKEL L 5 A_PlaySound("skeleton/pain")
    SKEL A 0 A_GiveInventory ("SkelAttack2", 1)
    SKEL A 0 A_SelectWeapon ("SkelAttack2")
    goto Spawn
  Death:
    SKEL LM 7
    SKEL N 7 A_PlaySound("skeleton/death")
    SKEL O 7 A_NoBlocking
    SKEL P 7
    SKEL Q -1
    stop
  Raise:
    SKEL QPONML 5
    goto Spawn

  PlayActiveRoar:
    SKEL A 0 A_PlaySound("skeleton/active")
    Goto See

  PlaySightRoar:
    SKEL A 0 A_PlaySound("skeleton/sight") // Rawr.
    Goto Spawn
  }
}

