actor SargAttack2 : MonsterWeapon
{
  Obituary "%o got bit by %k."
  States
  {
  Ready:
    TNT1 A 1 A_WeaponReady
    loop
  Deselect:
    TNT1 A 1 A_Lower
    loop
  Select:
    TNT1 A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    TNT1 A 0 A_Raise
    TNT1 A 0 A_Raise
    TNT1 A 0 A_Raise
    TNT1 A 1 A_Raise
    loop
  Fire:
    TNT1 A 0 A_PlaySound("demon/melee")
    TNT1 A 0 A_JumpIfInventory ("IsNightmare", 1, "NightmareFire")
    TNT1 AA 8
    TNT1 A 8 A_CustomPunch(4)
    goto Ready

  NightmareFire:
    TNT1 AA 4 // Nightmare Demon's attack speed is doubled
    TNT1 A 4 A_CustomPunch(4)
    goto Ready
  }

}

actor DemonPlayer : PlayerMonster
{
  player.startitem "SargAttack2"
  player.displayname "Demon"
  Player.SoundClass "demon2" // Custom sound classes used primarly for *usefail and *grunt.
  health 150
  radius 30
  height 56
  mass 400
  Speed 0.33
  FastSpeed 0.66 // For Nightmare
  painchance 200
  +FLOORCLIP
  States
  {
  Spawn:
	  SARG A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SARG A 0 Thing_ChangeTID(0, 1)
    SARG A 0 A_JumpIfInventory ("Clip", 1, 5) // If the Nightmare check occured, skip to the rest of the state.
    SARG A 0 A_GiveInventory ("Clip", 10) // We give the Demon a clip. If the clip is doubled, theres a definite possibility that its on the Nightmare difficulty.
    SARG A 0 A_JumpIfInventory ("Clip", 20, 2)
    SARG A 0 A_Jump (256, 2)
    SARG A 0 A_GiveInventory ("IsNightmare", 1) // It's on the Nightmare difficulty, used for the Demon attack.
    SARG A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    SARG A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    SARG A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    SARG A 0 A_ChangeFlag ("SHOOTABLE", 1)
    SARG A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    SARG A 0 A_GiveInventory ("SargAttack2") // Incase the map changed during the pain state, otherwise the weapon would be lost
    SARG A 0 A_SelectWeapon ("SargAttack2")
    SARG AB 10
    SARG A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    SARG A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    SARG A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    Loop
  See:
	  SARG A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SARG A 0 Thing_ChangeTID(0, 1)
    SARG AABBCCDD 2 Fast
    SARG D 0 A_Jump (20, "PlayActiveRoar")
    SARG D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    SARG D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    SARG D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    Loop
  Missile:
  Melee:
    SARG A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    SARG A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SARG EF 8 Fast
    SARG G 9 Fast
    SARG A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    Goto Spawn
  Pain:
    SARG A 0 A_TakeInventory ("SargAttack2", 1) // Take away weapon to stop the monster from firing while stunned
    SARG A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    SARG H 2 Fast
    SARG H 2 Fast A_PlaySound ("demon/pain")
    SARG A 0 A_GiveInventory ("SargAttack2", 1)
    SARG A 0 A_SelectWeapon ("SargAttack2")
    Goto Spawn
  Death:
    SARG I 8
    SARG J 8 A_Playsound ("demon/death")
    SARG K 4
    SARG L 4 A_NoBlocking
    SARG M 4
    SARG N -1
    Stop
  Raise:
    SARG N 5
    SARG MLKJI 5
    Goto Spawn

  PlayActiveRoar:
    SARG A 0 A_PlaySound("demon/active")
    Goto See

  PlaySightRoar:
    SARG A 0 A_PlaySound("demon/sight") // Rawr.
    Goto Spawn

  }
}


actor SpectrePlayer : DemonPlayer
{
  +SHADOW
  RenderStyle OptFuzzy
  Alpha 0.5
  player.displayname "Spectre"
}

actor IsNightmare : Inventory
{
	Game Doom
	Inventory.Amount 1
	Inventory.MaxAmount 1
}
