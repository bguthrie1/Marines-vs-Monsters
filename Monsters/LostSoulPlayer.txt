actor LostSoulBite : MonsterWeapon
{
  obituary "%o was spooked by %k."
  States
  {
  Ready:
    TNT1 A 1 A_WeaponReady
    loop
  Deselect:
    TNT1 A 1 A_Lower
    loop
  Select:
    TNT1 A 0 A_Raise
    TNT1 A 0 A_Raise
    TNT1 A 0 A_Raise
    TNT1 A 0 A_Raise
    TNT1 A 1 A_Raise
    loop
  Fire:
    TNT1 A 0 A_CheckFlag (SKULLFLY, "Ready")
    TNT1 A 10
    TNT1 A 0 A_PlaySound("skull/melee")
    TNT1 A 0 A_ChangeFlag ("SKULLFLY", 1)
    TNT1 A 8 A_Recoil (-20)
    Goto Ready
  }
  Obituary "%o was spooked by %k."
}

actor LostSoulPlayer : PlayerMonster
{
  player.startitem "LostSoulBite"
  player.displayname "Lost Soul"
  Player.SoundClass "skull2"
  player.viewheight 31
  obituary "%o was spooked by %k."
  health 100
  radius 16
  height 56
  mass 50
  speed 0.2
  painchance 256
  RenderStyle SoulTrans
  Damage 3
  +DONTFALL
  +NOICEDEATH
  +NOGRAVITY
  +FLOAT
  -SLIDESONWALLS
//  +FLOORCLIP
  States
  {
  Spawn:
	  SKUL A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SKUL A 0 Thing_ChangeTID(0, 1)
    SKUL A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    SKUL A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    SKUL A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    SKUL A 0 A_ChangeFlag ("SHOOTABLE", 1)
    SKUL A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    SKUL A 0 A_GiveInventory ("LostSoulBite") // Incase the map changed during the pain state, otherwise the weapon would be lost
    SKUL A 0 A_SelectWeapon ("LostSoulBite")
    SKUL A 0 SetPlayerProperty (0, 1, PROP_FLY) // Allows the monster to fly.
    SKUL A 0 SetPlayerProperty (0, 0, PROP_TOTALLYFROZEN) // This is placed here to enable the Lost Soul's attack again.
    SKUL AB 10 BRIGHT
    SKUL A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    SKUL A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    SKUL A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    Loop

  See:
	  SKUL A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SKUL A 0 Thing_ChangeTID(0, 1)
    SKUL A 0 A_CheckFlag (SKULLFLY, 2)
    SKUL A 0 SetPlayerProperty (0, 0, PROP_TOTALLYFROZEN) // This is placed here to enable the Lost Soul's attack again.
    SKUL AB 6 BRIGHT
    SKUL D 0 A_Jump (15, "PlayActiveRoar")
    SKUL D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    SKUL D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    SKUL D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    Loop

  Missile:
    SKUL A 0 SetPlayerProperty (0, 1, PROP_TOTALLYFROZEN) // Lost Souls cannot move or stop a charge when charging.
    SKUL A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SKUL C 10 BRIGHT
    SKUL D 4
    SKUL CD 4 BRIGHT
    Goto Missile+4

  Pain:
    SKUL A 0 A_TakeInventory ("LostSoulBite", 1) // Take away weapon to stop the monster firing while stunned
    SKUL E 3 BRIGHT
    SKUL E 3 BRIGHT A_PlaySound("skull/pain")
    SKUL A 0 A_GiveInventory ("LostSoulBite", 1)
    SKUL A 0 A_SelectWeapon ("LostSoulBite")
    Goto Spawn
  Death:
    SKUL F 6 BRIGHT
    SKUL G 6 BRIGHT A_PlayerScream
    SKUL H 6 BRIGHT
    SKUL I 6 BRIGHT A_NoBlocking
    SKUL J 6
    SKUL K 6
    TNT1 A -1 A_ChangeFlag("INVISIBLE",1)
    Stop

  PlayActiveRoar:
    BOSS A 0 A_PlaySound("skull/active")
    Goto See

  PlaySightRoar:
    SKUL A 0 A_PlaySound("skull/sight") // Roars when player finds a target
    Goto Spawn
  }
}
