actor CPosAttackSS : MonsterWeapon
{
  obituary "%o met %k."
  States
  {
  Ready:
    SHTG A 1 A_WeaponReady
    Loop
  Deselect:
    SHTG A 1 A_Lower
    Loop
  Select:
    SHTG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 1 A_Raise
    Loop
  Fire:
    SHTG A 10
    TNT1 A 0 A_ReFire
  Hold:
    SHTG A 10
    SHTG A 0 A_GunFlash
    SHTG A 0 A_PlaySound("chainguy/attack", CHAN_WEAPON)
    SHTG A 4 A_FireBullets (22.5, 0, 3, random(1,5)*3, "BulletPuff", FBF_NORANDOM, 0)
    SHTG A 6
    SHTG A 0 A_GunFlash
    SHTG A 0 A_PlaySound("chainguy/attack", CHAN_WEAPON)
    SHTG A 4 A_FireBullets (22.5, 0, 3, random(1,5)*3, "BulletPuff", FBF_NORANDOM, 0)
    SHTG A 0 A_ReFire
    Goto Ready
  Flash:
    SHTF A 4 Bright //A_Light1
    Goto LightDone
    SHTF A 4 Bright //A_Light0
    Goto LightDone
  }
}

actor WolfensteinSSPlayer : PlayerMonster
{
  player.startitem "CPosAttackSS"
  player.displayname "Wolfenstein SS"
  player.viewheight 48
  health 20
  radius 20
  height 56
  mass 100
  speed 0.2
  painchance 200
  +FLOORCLIP
  states
  {
  Spawn:
	  SSWV A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SSWV A 0 Thing_ChangeTID(0, 1)
    SSWV A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    SSWV A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    SSWV A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    SSWV A 0 A_ChangeFlag ("SHOOTABLE", 1)
    SSWV A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    SSWV A 0 A_GiveInventory ("CPosAttackSS") // Incase the map changed during the pain state, otherwise the weapon would be lost
    SSWV A 0 A_SelectWeapon ("CPosAttackSS")
    SSWV AB 10
    SSWV A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    SSWV A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    SSWV A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
	  SSWV A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SSWV A 0 Thing_ChangeTID(0, 1)
    SSWV AABBCCDD 3
    SSWV D 0 A_Jump (20, "PlayActiveRoar")
    SSWV D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    SSWV D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    SSWV D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Missile:
    SSWV A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    SSWV A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SSWV FF 10
  Melee:
    SSWV G 4 bright
    SSWV F 6
    SSWV G 4 bright
    SSWV F 1
    SSWV A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn
  Pain:
    SSWV A 0 A_TakeInventory ("CPosAttackSS", 1) // Take away weapon to stop the monster from firing while stunned
    SSWV A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    SSWV H 3
    SSWV H 3 A_PlaySound("wolfss/pain")
    SSWV A 0 A_GiveInventory ("CPosAttackSS", 1)
    SSWV A 0 A_SelectWeapon ("CPosAttackSS")
    goto Spawn
  Death:
    SSWV A 0 A_SpawnItemEx ("Clip", 0, 0, 0, 0, 0, 8)
    SSWV I 5
    SSWV J 5 A_PlaySound("wolfss/death")
    SSWV K 5 A_NoBlocking
    SSWV L 5
    SSWV M -1
    stop
  XDeath:
    SSWV A 0 A_SpawnItemEx ("Clip", 0, 0, 0, 0, 0, 8)
    SSWV N 5
    SSWV O 5 A_XScream
    SSWV P 5 A_NoBlocking
    SSWV QRSTU 5
    SSWV V -1
    stop
  Raise:
    SSWV MLKJI 5
    goto Spawn

  PlayActiveRoar:
    SSWV A 0 A_PlaySound("wolfss/active")
    Goto See

  PlaySightRoar:
    SSWV A 0 A_PlaySound("wolfss/sight") // Rawr.
    Goto Spawn
  }
}