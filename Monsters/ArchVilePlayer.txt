actor VileAIWeapon : MonsterWeapon
{
  Obituary "%o was incinerated by %k."
  states {

    Fire:
      TNT1 A 0 A_JumpIfInventory ("ArchIsHealing", 1, "Ready") // Don't launch an attack if the Archvile is or was healing (to prevent the heal then attack revived monster bug)
      TNT1 A 0 A_JumpIfTargetInLOS (1, 0, JLOSF_DEADNOJUMP, 896, 0) // If a monster is provoking you, or a player sees you, you may fire when ready.
      goto Ready
      TNT1 A 10 A_GunFlash
      TNT1 A 56 A_VileTarget // The Archvile attacks.
      TNT1 A 0 A_VileAttack
      TNT1 A 20
      Goto Ready


    Deselect:
      TNT1 A 1 A_Lower
      Loop

    Select:
      TNT1 A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
      TNT1 A 0 A_Raise
      TNT1 A 0 A_Raise
      TNT1 A 0 A_Raise
      TNT1 A 1 A_Raise
      Loop

    Ready:
      TNT1 A 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInTargetLOS (1, 0, JLOSF_DEADNOJUMP, 896, 0) // Not necessary to put additional checks because monsters never target the Archvile.
      goto Ready
      TNT1 A 0 A_SetBlend ("99, 99, 0", .2, 20) // If the Archvile's AI sees a valid target, make the screen flash yellow.
      Loop


  }
}

actor ArchIsHealing : Inventory
{
  Game Doom
  Inventory.Amount 1
  Inventory.MaxAmount 1
}

actor ArchVileHeal : Archvile
{
  // This actor is spawned by the Player Archvile to revive dead monsters.
  Speed 10
  -SOLID // Otherwise it wouldnt spawn because the player is in the way
  +NOCLIP // Or spawn in small corridors because of it's large radius
  +INVISIBLE // And that it's not seen
  -SHOOTABLE  // Or able to be shot
  radius 100 // Bigger radius means bigger AOE for Heal
  DeathSound "" // Make sure it can't make any noise.
  SeeSound ""
  PainSound ""
  DeathSound ""
  ActiveSound ""
  MeleeSound ""


  states
  {
   Spawn:
   VILE A 0 A_ChangeFlag ("FRIENDLY", 1) // So that it moves on its own
   Goto See

   See:
   VILE AABBCC 1 A_VileChase
   VILE A 0 A_Jump (256, "RemoveActor") // Removes itself if it doesn't find anything to heal
   loop

   Missile:
   Stop // DON'T FU*KING ATTACK PLS YOU'RE A PROP

   Heal:
   VILE A 0 A_ChangeFlag ("FRIENDLY", 0) // So that it doesn't revive monsters into friendly ones.
   VILE "[\]" 10 Bright
   VILE A 0 A_Jump (256, "RemoveActor")
    goto Spawn

   RemoveActor:
   VILE A 0
   Stop
  }

}

actor ArchvilePlayerAITargeting : PlayerMonster
{

  health 700
  radius 20
  height 56
  mass 500
  speed 0.5
  painchance 10
  player.viewheight 50
  player.displayname "Archvile _AI Targeting_"
  player.startitem "VileAIWeapon"
  Player.SoundClass "vile2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o was incinerated by %k."
  DamageFactor "Fire", 0
  +FLOORCLIP
  +NOTARGET // Monsters wont' target the Archvile.
//  +QUICKTORETALIATE // Able to switch targets on the fly. Not needed.

  states
  {
  Spawn:
	  VILE A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    VILE A 0 Thing_ChangeTID(0, 1)
    VILE A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    VILE A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    VILE A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    VILE A 0 A_ChangeFlag ("SHOOTABLE", 1)
    VILE A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    VILE A 0 A_GiveInventory ("VileAIWeapon") // Incase the map changed during the pain state, otherwise the weapon would be lost
    VILE A 0 A_SelectWeapon ("VileAIWeapon")
    VILE A 0 A_TakeInventory ("ArchIsHealing", 1) // I'm pretty sure the Archvile is done healing at this point.
    VILE AB 10
    VILE A 0 A_JumpIfTargetInLOS(2, 0, JLOSF_DEADNOJUMP, 896, 0) // If a target is in LOS, do an A_Look which will give the AI targeting a target. If not, forget the target. Will cause problems otherwise.
    VILE A 0 A_ClearTarget
    Goto Spawn
    VILE A 0 A_LookEx("LOF_NOJUMP", 0, 0, 0, 0, 1) // Needed to get a target for the ArchVile. Don't jump to the See state however so that it can play the sight roar.
    VILE A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    VILE A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    VILE A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop


  See:
	  VILE A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    VILE A 0 Thing_ChangeTID(0, 1)
    VILE AABBCCDDEEFF 2
    VILE F 0 A_TakeInventory ("ArchIsHealing", 1) // I'm pretty sure the Archvile has stopped healing at this point.
    VILE F 0 A_LookEx("LOF_NOJUMP", 0, 0, 0, 0, 1) // Needed to get a target for the ArchVile. Don't restart the See state, else it wouldn't be able to play the Active Roar.
    VILE D 0 A_Jump (20, "PlayActiveRoar")
    VILE D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    VILE D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    VILE D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop

  Missile:
  VILE A 0 A_JumpIfTargetInLOS("Melee", 0, JLOSF_NOSIGHT|JLOSF_DEADNOJUMP, 896, 0) // If theres a target in LOS and the player pressed the attack button, fire the Archvile's Deathstar. Else, attempt to heal a dead monster.
  VILE A 0 A_Jump (256, "Heal")
  goto Spawn

  Melee:
  VILE A 0 A_JumpIfInventory ("ArchIsHealing", 1, "Heal") // Don't light a fire under someone's ass if you're trying to heal someone. Duh. Sometimes it helps though.
  VILE A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
  VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
  VILE G 0 A_VileStart
  VILE G 10 BRIGHT
  VILE HIJKLMNO 8 BRIGHT
  VILE P 20 BRIGHT
  VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
  VILE A 0 A_ClearTarget
  VILE A 0 A_Look // Regain a new target after firing.
  goto Spawn

  Heal:
   VILE A 0 A_GiveInventory ("ArchIsHealing", 1)
   VILE A 0 A_SetBlend ("0, 99, 0", 0.1, 20) // Screen flashes green if Archvile is healing.
   VILE A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
   VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
   VILE A 0 A_SpawnItemEx ("ArchVileHeal")
   VILE "[\]" 10 BRIGHT
   VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
   VILE A 0 A_TakeInventory ("ArchIsHealing", 1)
   goto Spawn

  Pain:
    VILE A 0 A_TakeInventory ("ArchIsHealing", 1)
    VILE A 0 A_TakeInventory ("VileAIWeapon", 1) // Take away weapon to stop the monster firing while stunned
    VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    VILE Q 5
    VILE Q 5 A_PlaySound("vile/pain")
    VILE A 0 A_GiveInventory ("VileAIWeapon", 1)
    VILE A 0 A_SelectWeapon ("VileAIWeapon")
    goto Spawn

  Death:
    VILE Q 7
    VILE R 7 A_PlayerScream
    VILE S 7 A_NoBlocking
    VILE TUVWXY 7
    VILE Z -1
    stop

  PlayActiveRoar:
    VILE A 0 A_PlaySound("vile/active") // Hai gais, im still here
    goto See

  PlaySightRoar:
    VILE A 0 A_PlaySound("vile/sight") // Rawr.
    Goto Spawn
  }
}

actor VileWeapon : MonsterWeapon
{
  Obituary "%o was incinerated by %k."
  states {
    Fire:
      TNT1 A 0 A_JumpIfInventory ("ArchIsHealing", 1, "Ready") // Don't launch an attack if the Archvile is or was healing (to prevent the heal then attack revived monster bug)
      TNT1 A 0 A_JumpIfTargetInLOS(1)
      Goto Ready
      TNT1 A 10 A_GunFlash
      TNT1 A 56 A_FireCustomMissile("VilePlayerFire")
      TNT1 A 20
      Goto Ready
    Select:
      TNT1 A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
      TNT1 A 0 A_Raise
      TNT1 A 0 A_Raise
      TNT1 A 0 A_Raise
      TNT1 A 1 A_Raise
      Loop
    Deselect:
      TNT1 A 1 A_Lower
      Loop
    Ready:
      TNT1 A 1 A_WeaponReady
      Loop
  }
}

actor VilePlayerFire
{
  Projectile
  RenderStyle Add
  Alpha 1
  DamageType Fire
  +SEEKERMISSILE
  +NOBLOCKMAP
  +NOGRAVITY
  +OLDRADIUSDMG
  States
  {
  Spawn:
    TNT1 AA 0 A_Fire
    FIRE A 2 bright A_StartFire
    FIRE BAB 2 bright A_Fire
    FIRE C 2 bright A_FireCrackle
    FIRE BCBCDCDCDEDED 2 bright A_Fire
    FIRE E 2 bright A_FireCrackle
    FIRE FEFEFGHGH 2 bright A_Fire
    FIRE G 0 bright A_Fire
    FIRE G 0 bright A_JumpIfTargetInLOS("DoAttack")
    FIRE GH 2 bright A_Fire
    Stop
  DoAttack:
    FIRE G 0 bright A_Fire
    FIRE G 0 bright A_PlaySound("vile/stop")
    FIRE GGGGG 0 bright A_SeekerMissile(90,90)
    FIRE G 0 bright A_CustomMissile("VilePlayer20Points",1,0,0,6)
    FIRE G 0 bright A_Explode(70, 70, 0)
    FIRE G 0 bright A_RadiusThrust(1000,20)
    FIRE GH 2 bright A_Fire
    Stop
  StopAttack:
    Stop
  }
}

actor VilePlayer20Points {
  Projectile
  Speed 1000
  Damage (20)
  DamageType Fire

  states {
  Spawn:
    TNT1 A 1
    loop
  }
}

actor ArchvilePlayer : PlayerMonster
{

  health 700
  radius 20
  height 56
  mass 500
  speed 0.5
  painchance 10
  player.viewheight 50
  player.displayname "Archvile"
  player.startitem "VileWeapon"
  Player.SoundClass "vile2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o was incinerated by %k."
  DamageFactor "Fire", 0
  +FLOORCLIP
  +NOTARGET

  states
  {
  Spawn:
	  VILE A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    VILE A 0 Thing_ChangeTID(0, 1)
    VILE A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    VILE A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    VILE A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    VILE A 0 A_ChangeFlag ("SHOOTABLE", 1)
    VILE A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    VILE A 0 A_GiveInventory ("VileWeapon") // Incase the map changed during the pain state, otherwise the weapon would be lost
    VILE A 0 A_SelectWeapon ("VileWeapon")
    VILE A 0 A_TakeInventory ("ArchIsHealing", 1) // Archvile is done healing at this point.
    VILE AB 10
    VILE A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    VILE A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    VILE A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop


  See:
	  VILE A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    VILE A 0 Thing_ChangeTID(0, 1)
    VILE AABBCCDDEEFF 2
    VILE F 0 A_TakeInventory ("ArchIsHealing", 1) // Archvile has stopped healing at this point.
    VILE F 0 A_Look // Needed to get a target for the ArchVile.
    VILE D 0 A_Jump (20, "PlayActiveRoar")
    VILE D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    VILE D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    VILE D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop

  Missile:
  VILE A 0 A_JumpIfTargetInLOS("Melee", 0, JLOSF_NOSIGHT|JLOSF_DEADNOJUMP, 896, 0) // If theres a target in LOS and the player pressed the attack button, fire the Archvile's attack. Else, attempt to heal a dead monster.
  VILE A 0 A_Jump (256, "Heal")
  goto Spawn

  Melee:
  VILE A 0 A_JumpIfInventory ("ArchIsHealing", 1, "Heal") // Don't light a fire under someone's ass if you're trying to heal someone. Duh. Sometimes it helps though.
  VILE A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
  VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
  VILE G 0 A_VileStart
  VILE G 10 BRIGHT
  VILE HIJKLMNO 8 BRIGHT
  VILE P 20 BRIGHT
  VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
  goto Spawn

  Heal:
   VILE A 0 A_GiveInventory ("ArchIsHealing", 1)
   VILE A 0 A_SetBlend ("0, 99, 0", 0.1, 20) // Screen flashes green if Archvile is healing.
   VILE A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
   VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
   VILE A 0 A_SpawnItemEx ("ArchVileHeal")
   VILE "[\]" 10 BRIGHT
   VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
   VILE A 0 A_TakeInventory ("ArchIsHealing", 1)
   goto Spawn

  Pain:
    VILE A 0 A_TakeInventory ("ArchIsHealing", 1)
    VILE A 0 A_TakeInventory ("VileWeapon", 1) // Take away weapon to stop the monster firing while stunned
    VILE A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    VILE A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    VILE Q 5
    VILE Q 5 A_PlaySound("vile/pain")
    VILE A 0 A_GiveInventory ("VileWeapon", 1)
    VILE A 0 A_SelectWeapon ("VileWeapon")
    goto Spawn

  Death:
    VILE Q 7
    VILE R 7 A_PlayerScream
    VILE S 7 A_NoBlocking
    VILE TUVWXY 7
    VILE Z -1
    stop

  PlayActiveRoar:
    VILE A 0 A_PlaySound("vile/active") // Hai gais, im still here
    goto See

  PlaySightRoar:
    VILE A 0 A_PlaySound("vile/sight") // Rawr.
    Goto Spawn
  }
}

