actor CyberAttack : MonsterWeapon
{
  Obituary "%o got splattered by %k."
  states
  {
  Ready:
    MISG A 1 A_WeaponReady
    loop
  Deselect:
    MISG A 1 A_Lower
    loop
  Select:
    MISG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    MISG A 0 A_Raise
    MISG A 0 A_Raise
    MISG A 0 A_Raise
    MISG A 1 A_Raise
    loop
  Fire:
    MISG B 8 A_GunFlash
    MISG B 12 A_FireCustommissile("RocketFF")
    MISG B 12 A_GunFlash
    MISG B 12 A_FireCustommissile("RocketFF")
    MISG B 12 A_GunFlash
    MISG B 12 A_FireCustommissile("RocketFF")
    MISG B 0 A_ReFire
    goto ready
  Flash:
    MISF A 3 bright //A_Light1
    MISF B 4 bright //A_Light2
    MISF CD 4 bright //A_Light0
    stop

  }
}

actor CyberDemonPlayer : PlayerMonster
{
  Health 4000
  Radius 40
  Height 110
  Mass 1000
  Speed 0.4 // 16/4
  Painchance 20
  Player.ViewHeight 100
  Player.Displayname "CyberDemon" // Name shown on menu display.
  Player.Startitem "CyberAttack" // So that the monster player can shoot stuff.
//  Player.GruntSpeed 1 // The hoof sound when the cyberdemon lands is played even if he goes off a ledge slowly. Cannot use in the current release of ZDoom.
  Player.SoundClass "cyber2" // Custom sound classes used primarly for *usefail and *grunt.
  DamageFactor "CyberFire", 0
  +BOSS
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH // It's a boss, he shouldn't morph from a weapon or by anything else.
  +FIXMAPTHINGPOS // Incase he gets stuck when he spawns in a map.

  states
  {

  Spawn:
	  CYBR A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    CYBR A 0 Thing_ChangeTID(0, 1)
    CYBR A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    CYBR A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    CYBR A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    CYBR A 0 A_ChangeFlag ("SHOOTABLE", 1)
    CYBR A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    CYBR A 0 A_GiveInventory ("CyberAttack") // Incase the map changed during the pain state, otherwise the weapon would be lost
    CYBR A 0 A_SelectWeapon ("CyberAttack")
    CYBR AB 10
    CYBR A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    CYBR A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    CYBR A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop

  See:
	  CYBR A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    CYBR A 0 Thing_ChangeTID(0, 1)
    CYBR A 3 A_PlaySound("cyber/hoof") // Needed for Cyberdemon's stepping sound
    CYBR ABBCC 3
    CYBR D 3 A_PlaySound("spider/walk") // See above
    CYBR D 3
    CYBR D 0 A_Jump (20, "PlayActiveRoar")
    CYBR D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    CYBR D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    CYBR D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop


  Missile:
    CYBR A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    CYBR A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    CYBR E 6
    CYBR F 12
    CYBR E 12
    CYBR F 12
    CYBR E 12
    CYBR F 12
    CYBR F 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn

  Pain:
    CYBR A 0 A_TakeInventory ("CyberAttack", 1) // Take away weapon to stop the monster firing while stunned
    CYBR A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    CYBR G 10 A_PlaySound("cyber/pain")
    CYBR G 0 A_GiveInventory ("CyberAttack", 1)
    CYBR G 0 A_SelectWeapon ("CyberAttack")
    goto Spawn

  Death:
    CYBR H 10
    CYBR I 10 A_PlaySound("cyber/death", 4, 1.0, 0, ATTN_NONE)
    CYBR JKL 10
    CYBR M 10 A_NoBlocking
    CYBR NO 10
    CYBR P 30
    CYBR P -1
    Stop

  PlayActiveRoar:
    CYBR A 0 A_PlaySound("cyber/active")
    Goto See

  PlaySightRoar:
    CYBR A 0 A_PlaySound("cyber/sight", 4, 1.0, 0, ATTN_NONE) // Rawr.
    Goto Spawn

  }
}

