actor FatRocketLauncher : MonsterWeapon
{
  Obituary "%o got splattered by %k."
  states {
    Ready:
    MISG A 1 A_WeaponReady
    loop

    Deselect:
    MISG A 1 A_Lower
    loop

    Select:
    MISG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    MISG A 0 A_Raise
    MISG A 0 A_Raise
    MISG A 0 A_Raise
    MISG A 1 A_Raise
    loop

    Fire:
      MISG B 20 A_PlaySound("fatso/raiseguns")
      MISG A 0 A_GunFlash
      MISG B 0 A_FireCustomMissile("FatShot")
      MISG B 20 A_FireCustomMissile("FatShot",11.25)
      MISG A 0 A_GunFlash
      MISG B 0 A_FireCustomMissile("FatShot")
      MISG B 20 A_FireCustomMissile("FatShot",-22.5)
      MISG A 0 A_GunFlash
      MISG B 0 A_FireCustomMissile("FatShot",-5.625)
      MISG B 23 A_FireCustomMissile("FatShot",5.625) // three extra tics to match attack on nightmare
      Goto Ready

    Flash:
    MISF A 3 bright //A_Light1
    MISF B 4 bright //A_Light2
    MISF CD 4 bright //A_Light0
    stop
  }
}

actor MancubusPlayer : PlayerMonster
{
  health 600
  radius 48
  height 64
  mass 1000
  speed 0.2 // 8/4
  painchance 80
  player.viewheight 60
  player.displayname "Mancubus"
  player.startitem "fatrocketlauncher"
  Player.SoundClass "fatso2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o got splattered by %k."
  DamageFactor "FatFire", 0
  +FLOORCLIP
  +DONTMORPH // It's kinda a boss, he shouldn't morph from a weapon or by anything else.

  states
  {
  Spawn:
	  FATT A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    FATT A 0 Thing_ChangeTID(0, 1)
    FATT A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    FATT A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    FATT A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    FATT A 0 A_ChangeFlag ("SHOOTABLE", 1)
    FATT A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    FATT A 0 A_GiveInventory ("FatRocketLauncher") // Incase the map changed during the pain state, otherwise the weapon would be lost
    FATT A 0 A_SelectWeapon ("FatRocketLauncher")
    FATT AB 15
    FATT A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    FATT A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    FATT A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
	  FATT A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    FATT A 0 Thing_ChangeTID(0, 1)
    FATT AABBCCDDEEFF 4
    FATT D 0 A_Jump (20, "PlayActiveRoar")
    FATT D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    FATT D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    FATT D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Missile:
    FATT A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    FATT A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    FATT G 20
    FATT H 10 BRIGHT
    FATT IG 5
    FATT H 10 BRIGHT
    FATT IG 5
    FATT H 10 BRIGHT
    FATT IG 5
    FATT A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    Goto Spawn
  Pain:
    FATT A 0 A_TakeInventory ("FatRocketLauncher", 1) // Take away weapon to stop the monster firing while stunned
    FATT A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    FATT J 3
    FATT J 3 A_PlaySound("fatso/pain")
    FATT A 0 A_GiveInventory ("FatRocketLauncher", 1)
    FATT A 0 A_SelectWeapon ("FatRocketLauncher")
    goto Spawn
  Death:
    FATT K 6
    FATT L 6 A_PlayerScream
    FATT M 6 A_NoBlocking
    FATT NOPQRS 6
    FATT T -1
    stop
  Raise:
    FATT R 5
    FATT QPONMLK 5
    Goto See

  PlayActiveRoar:
    FATT A 0 A_PlaySound("fatso/active")
    Goto See

  PlaySightRoar:
    FATT A 0 A_PlaySound("fatso/sight") // Rawr.
    Goto Spawn
  }
}

