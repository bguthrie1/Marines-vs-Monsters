actor PosAttack2 : MonsterWeapon
{
  States
  {
  Ready:
    SHTG A 1 A_WeaponReady
    Loop
  Deselect:
    SHTG A 1 A_Lower
    Loop
  Select:
    SHTG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_ReFire
  Hold:
    SHTG A 10
    SHTG A 0 A_PlaySound ("grunt/attack")
    SHTG A 0 A_GunFlash
    SHTG A 8 A_FireBullets (22.5, 0, 1, random(1,5)*3, "BulletPuff", FBF_NORANDOM, 0)
    SHTG A 11 // Added three more tics to match the Nightmare animation (would throw off the animations otherwise).
    SHTG A 0 A_ReFire
    Goto Ready
  Flash:
    SHTF A 4 Bright //A_Light1
    SHTF B 3 Bright //A_Light2
    Goto LightDone
  }
}

actor ZombieManPlayer : PlayerMonster
{
  player.startitem "PosAttack2"
  player.displayname "Zombieman"
  Player.Viewheight 48
  health 20
  radius 20
  height 56
  mass 100
  speed 0.2
  painchance 200
  +FLOORCLIP
  states
  {
  Spawn:
	  POSS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    POSS A 0 Thing_ChangeTID(0, 1)
    POSS A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    POSS A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    POSS A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    POSS A 0 A_ChangeFlag ("SHOOTABLE", 1)
    POSS A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    POSS A 0 A_GiveInventory ("PosAttack2") // Incase the map changed during the pain state, otherwise the weapon would be lost
    POSS A 0 A_SelectWeapon ("PosAttack2")
    POSS AB 10
    POSS A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    POSS A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    POSS A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
	  POSS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    POSS A 0 Thing_ChangeTID(0, 1)
    POSS AABBCCDD 4
    POSS D 0 A_Jump (20, "PlayActiveRoar")
    POSS D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    POSS D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    POSS D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Missile:
    POSS A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    POSS A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    POSS E 10
    POSS F 8
    POSS E 8
    POSS A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn
  Pain:
    POSS A 0 A_TakeInventory ("PosAttack2", 1) // Take away weapon to stop the monster from firing while stunned
    POSS A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    POSS G 3
    POSS G 3 A_PlaySound("grunt/pain")
    POSS A 0 A_GiveInventory ("PosAttack2", 1)
    POSS A 0 A_SelectWeapon ("PosAttack2")
    goto Spawn
  Death:
    POSS A 0 A_SpawnItemEx ("Clip", 0, 0, 0, 0, 0, 8)
    POSS H 5
    POSS I 5 A_PlaySound("grunt/death")
    POSS J 5 A_NoBlocking
    POSS K 5
    POSS L -1
    stop
  XDeath:
    POSS A 0 A_SpawnItemEx ("Clip", 0, 0, 0, 0, 0, 8)
    POSS M 5
    POSS N 5 A_XScream
    POSS O 5 A_NoBlocking
    POSS PQRST 5
    POSS U -1
    stop
  Raise:
    POSS KJIH 5
    goto Spawn

  PlayActiveRoar:
    POSS A 0 A_PlaySound("grunt/active")
    Goto See

  PlaySightRoar:
    POSS A 0 A_PlaySound("grunt/sight") // Rawr.
    Goto Spawn
  }
}