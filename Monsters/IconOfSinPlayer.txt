actor CubeShooter2 : MonsterWeapon
{
   Obituary "%o was spawned cubed by %k."
   States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 1 A_Raise
    loop
  Fire:
    PUNG A 150 A_FireCustomMissile("CubeSpawn")
    goto Ready
  }
}

actor IconOfSinPlayer : PlayerMonster
{
  Player.Startitem "CubeShooter2"
  Player.Displayname "Icon Of Sin"
  Obituary "%o was spawned cubed by %k."
  Health 250
  Mass 10000000
  PainChance 255
  +NOICEDEATH
  +OLDRADIUSDMG
  PainSound "brain/pain"
  DeathSound "brain/death"

  States
  {
  Spawn:
	  BBRN A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    BBRN A 0 Thing_ChangeTID(0, 1)
    BBRN A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    BBRN A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    BBRN A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    BBRN A 0 A_ChangeFlag ("SHOOTABLE", 1)
    BBRN A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    BBRN A 0 A_GiveInventory ("CubeShooter2") // Incase the map changed during the pain state, otherwise the weapon would be lost
    BBRN A 0 A_SelectWeapon ("CubeShooter2")
    BBRN A 0 SetPlayerProperty (0, 1, PROP_FLY) // Allows the monster to fly.
    BBRN A 1
    BBRN A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    BBRN A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    BBRN A 0 A_Jump (1, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    Goto Spawn
  Missile:
    BBRN A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    BBRN A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    BBRN A 150
    BBRN A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    Goto Spawn
  Pain:
    BBRN A 0 A_TakeInventory ("CubeShooter2", 1) // Take away weapon to stop the monster from firing while stunned
    BBRN A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    BBRN B 36 A_PlaySound("brain/pain", 4, 1.0, 0, ATTN_NONE)
    BBRN A 0 A_GiveInventory ("CubeShooter2", 1)
    BBRN A 0 A_SelectWeapon ("CubeShooter2")
    Goto Spawn
  Death:
    BBRN A 0 A_PlaySound("brain/death", 4, 1.0, 0, ATTN_NONE)
    BBRN A 0 A_NoBlocking
    SKUL F 25 Bright
    SKUL G 25 Bright
    SKUL H 25 Bright
    SKUL I 15 Bright
    SKUL J 15 Bright
    SKUL K 15 Bright
    TNT1 A -1 A_ChangeFlag("INVISIBLE",1)
    Stop

  PlaySightRoar:
    BBRN A 0 A_PlaySound("brain/sight", 4, 1.0, 0, ATTN_NONE) // Rawr.
    Goto Spawn
  }
}


actor CubeSpawn : SpawnShot
{
    -NOCLIP
   Damage 10000000 // Already does telefragging damage, but this is so that the player can get the kill.
   States
{
   Spawn:
   BOSF A 3 Bright
   BOSF BCD 3 Bright
   loop
   Death:
   FIRE A 0 A_PlaySound("misc/teleport")
   FIRE A 0 A_SpawnItemEx ("CubeFire", 0, 0, 0, 0, 0, 0, 0, SXF_TELEFRAG)
   FIRE A 0 A_SetTranslucent (1.0, 1)
   FIRE ABCDEFGH 4 Bright A_Fire
   Stop
}

}

actor CubeFire : RandomSpawner
{
  DropItem "DoomImp" 		255 50
  DropItem "Demon"		255 40
  DropItem "Spectre"		255 30
  DropItem "PainElemental"	255 10
  DropItem "Cacodemon"		255 30
  DropItem "Archvile"		255  2
  DropItem "Revenant"		255 10
  DropItem "Arachnotron"	255 20
  DropItem "Fatso"		255 30
  DropItem "HellKnight"		255 24
  DropItem "BaronOfHell"	255 10
}
