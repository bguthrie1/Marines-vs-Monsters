actor SPosAttack2 : MonsterWeapon
{
  obituary "%o was shot by %k."
  States
  {
  Ready:
    SHTG A 1 A_WeaponReady
    Loop
  Deselect:
    SHTG A 1 A_Lower
    Loop
  Select:
    SHTG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 0 A_Raise
    SHTG A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_ReFire
  Hold:
    SHTG A 3
    SHTG A 0 A_PlaySound("shotguy/attack", CHAN_WEAPON)
    SHTG A 0 A_GunFlash
    SHTG A 7 A_FireBullets (22.5, 0, 3, random(1,5)*3, "BulletPuff", FBF_NORANDOM, 0)
    SHTG BC 3
    SHTG D 4
    SHTG CB 3
    SHTG A 6 // Added three more tics to match the Nightmare animation (would throw off the animations otherwise).
    SHTG A 0 A_ReFire
    Goto Ready
  Flash:
    SHTF A 4 Bright //A_Light1
    SHTF B 3 Bright //A_Light2
    Goto LightDone
  }
}

actor ShotgunGuyPlayer : PlayerMonster
{
  player.startitem "SPosAttack2"
  player.displayname "Shotgun Guy"
  player.viewheight 48
  health 30
  radius 20
  height 56
  mass 100
  speed 0.2
  painchance 200
  +FLOORCLIP
  states
  {
  Spawn:
	  SPOS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SPOS A 0 Thing_ChangeTID(0, 1)
    SPOS A 0 A_JumpIfInventory ("NoTargetAlreadySet", 1, 5)
    SPOS A 0 A_ChangeFlag ("SHOOTABLE", 0)  // It's used so that monsters who saw you instantly spawn wouldn't target you.
    SPOS A 0 SetPlayerProperty (0, 1, PROP_NOTARGET) // Monsters ignore the player monster. The player can still provoke them though, naturally.
    SPOS A 0 A_ChangeFlag ("SHOOTABLE", 1)
    SPOS A 0 A_GiveInventory("NoTargetAlreadySet", 1)
    SPOS A 0 A_GiveInventory ("SPosAttack2") // Incase the map changed during the pain state, otherwise the weapon would be lost
    SPOS A 0 A_SelectWeapon ("SPosAttack2")
    SPOS AB 10
    SPOS A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    SPOS A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    SPOS A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
	  SPOS A 0 A_JumpIf (tid == 1, 2) // TID 1 is the Demon team, TID 2 is the Marine team. Used for MVM.
    SPOS A 0 Thing_ChangeTID(0, 1)
    SPOS AABBCCDD 3
    SPOS D 0 A_Jump (20, "PlayActiveRoar")
    SPOS D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    SPOS D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    SPOS D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Missile:
    SPOS A 0 SetPlayerProperty (0, 1, PROP_FROZEN) // Stops movement of the monster when he fires. This is to mimic a normal monster attack.
    SPOS A 0 A_ScaleVelocity (0) // Stops the monster completely when the monster fires.
    SPOS E 10
    SPOS F 10 bright
    SPOS E 10
    SPOS A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // Monster can move again.
    goto Spawn
  Pain:
    SPOS A 0 A_TakeInventory ("SPosAttack2", 1) // Take away weapon to stop the monster from firing while stunned
    SPOS A 0 SetPlayerProperty (0, 0, PROP_FROZEN) // To ensure that the player can move after shooting if he is in pain while shooting.
    SPOS G 3
    SPOS G 3 A_PlaySound("shotguy/pain")
    SPOS A 0 A_GiveInventory ("SPosAttack2", 1)
    SPOS A 0 A_SelectWeapon ("SPosAttack2")
    goto Spawn
  Death:
    SPOS A 0 A_SpawnItemEx ("Shotgun", 0, 0, 0, 0, 0, 8)
    SPOS H 5
    SPOS I 5 A_PlaySound("shotguy/death")
    SPOS J 5 A_NoBlocking
    SPOS K 5
    SPOS L -1
    stop
  XDeath:
    SPOS A 0 A_SpawnItemEx ("Shotgun", 0, 0, 0, 0, 0, 8)
    SPOS M 5
    SPOS N 5 A_XScream
    SPOS O 5 A_NoBlocking
    SPOS PQRST 5
    SPOS U -1
    stop
  Raise:
    SPOS LKJIH 5
    goto Spawn

  PlayActiveRoar:
    SPOS A 0 A_PlaySound("shotguy/active")
    Goto See

  PlaySightRoar:
    SPOS A 0 A_PlaySound("shotguy/sight") // Rawr.
    Goto Spawn
  }
}